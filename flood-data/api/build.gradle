plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.7'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.openapi.generator' version '7.16.0'
}

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.data:spring-data-commons'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.9'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
    implementation 'org.projectlombok:lombok:1.18.38'
}

sourceSets.main.java.srcDirs += ['generated/src/main/java']

openApiGenerate {
    generatorName = "spring"
    inputSpec = "$rootDir/flood-data/api/open-api-specification.yml".toString()
    outputDir = "$rootDir/flood-data/api/generated".toString()
    apiPackage = "dev.jbriggs.flood.api"
    invokerPackage = "dev.jbriggs.flood.invoker"
    modelPackage = "dev.jbriggs.flood.model"

    configOptions = [
            generateBuilders    : "true",
            interfaceOnly       : "true",
            serializableModel   : "true",
            skipDefaultInterface: "true",
            useJakartaEe        : "true",
            useSpringBoot3      : "true",
            useTags             : "true"
    ]
}

import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

tasks.register('openApiGenerateSwaggerOnly', GenerateTask) {
    generatorName = "html2"
    inputSpec = "$rootDir/flood-data/api/open-api-specification.yml"
    outputDir = layout.buildDirectory.dir("generated/swagger-ui").get().asFile.toString()
    skipValidateSpec = false
    configOptions = [
            "hideDownloadButton": "true",
            "theme"             : "flattop"
    ]
}

compileJava.dependsOn 'openApiGenerate'

clean {
    delete "$projectDir/generated"
}

// Disable for overall project when running a build
tasks.named('bootJar') {
    enabled = false
}

tasks.named('bootRun') {
    enabled = false
}